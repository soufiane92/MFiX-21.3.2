# Data Index
#                     A-B
#  * En = 1.0    --  01
#  * En = 0.9    --  02
#  * En = 0.8    --  03
#  * En = 0.7    --  04
#  * En = 0.6    --  05
#  * En = 0.5    --  06
#                        

# color definitions
set border linewidth 1.5
set style line 1 lt 1 lc rgb '#000000' lw 1 # black
set style line 2 lt 1 lc rgb '#9400d3' lw 1 # purple
set style line 3 lt 1 lc rgb '#ff4500' lw 1 # orange-1
set style line 4 lt 1 lc rgb '#006400' lw 1 # green
set style line 5 lt 1 lc rgb '#ffa500' lw 1 # yellow
set style line 6 lt 1 lc rgb '#0000ff' lw 1 # blue
set style line 7 lt 1 lc rgb '#800000' lw 1 # brown
set style line 8 lt 1 lc rgb '#ff0000' lw 1 # orange-2

set style line 11 lt 2 lc rgb '#000000' lw 1 # black
set style line 12 lt 2 lc rgb '#9400d3' lw 1 # purple
set style line 13 lt 2 lc rgb '#ff4500' lw 1 # orange-1
set style line 14 lt 2 lc rgb '#006400' lw 1 # green
set style line 15 lt 2 lc rgb '#ffa500' lw 1 # yellow
set style line 16 lt 2 lc rgb '#0000ff' lw 1 # blue
set style line 17 lt 2 lc rgb '#800000' lw 1 # brown
set style line 18 lt 2 lc rgb '#ff0000' lw 1 # orange-2


set xlabel "friction coefficient, {/Symbol m}"
set xrange [0.1 : 1.1]
set xtics nomirror 0.2,0.2,1.0

scaled(x,val) = ( scld = x*val, scld)

#-------------------------------------------------------------------------------------

#set terminal wxt 1 size 900,400 enhanced dashed
set terminal pngcairo size 900,400 enhanced font 'Verdana, 10' dashed
set output "DEM04_all.png"

set multiplot layout 1,2 title 

set ylabel "Non-dimensional slip time end"
set yrange [0.28 : 0.29]
set ytics nomirror 0.28,0.002,0.29

set key inside left top

set title 
plot 'POST_TIME.dat' using 1:2 w lines ls 1 notitle, \
     'POST_TIME.dat' using 1:3 w points pointtype 4 \
        lc rgb '#000000' title "t_s /({/Symbol m}g/u_0)"

set ylabel "Non-dimensional velocities"
set yrange [0.714 : 0.7150]
set ytics nomirror 0.714,0.0002,0.715

set title
plot 'POST_TVEL.dat' using 1:2 w lines ls 1 notitle, \
     'POST_TVEL.dat' using 1:3 w points pointtype 6 \
        lc rgb '#000000' title "u/u_0", \
     'POST_AVEL.dat' using 1:3 w points pointtype 8 \
        lc rgb '#000000' title "{/Symbol w}r_p/u_0"

unset multiplot

